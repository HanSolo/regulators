import java.text.SimpleDateFormat

plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.google.osdetector'           version '1.7.0'
    id 'org.javamodularity.moduleplugin' version '1.8.11'
    id 'org.beryx.jlink'                 version '2.25.0'
    id 'net.nemerosa.versioning'         version '3.0.0'
}


apply plugin: 'maven-publish'
apply plugin: 'signing'


description   = 'Regulators is a little JavaFX library of controls'
mainClassName = "$moduleName/eu.hansolo.regulators.Main"


Date buildTimeAndDate = new Date()
ext {
    buildDate       = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime       = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    platform        = osdetector.os == 'osx' ? osdetector.arch == 'aarch_64' ? 'mac-aarch64' : 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os == 'linux' ? osdetector.arch == 'aarch_64' ? 'linux-aarch64' : 'linux' : osdetector.os
    ciOssrhUsername = System.getenv('OSSRH_USERNAME')
    ciOssrhPassword = System.getenv('OSSRH_PASSWORD')
    ciGHUser        = System.getenv('GH_USER')
    ciGHToken       = System.getenv('GH_TOKEN')
    gpgkey          = System.getenv("GPG_PRIVATE_KEY")
    gpgpassphrase   = System.getenv("PASSPHRASE")
}


repositories {
    mavenCentral()
}


dependencies {
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation 'org.kordamp.ikonli:ikonli-core:11.5.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:11.5.0'
    implementation 'org.kordamp.ikonli:ikonli-material-pack:11.5.0'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign-pack:11.5.0'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome-pack:11.5.0'
    implementation 'org.kordamp.ikonli:ikonli-weathericons-pack:11.5.0'
}


jar {
    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'eu.hansolo.regulators',
                'Export-Package'        : 'eu.hansolo.regulators'
        )
    }
}


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


// start the app from gradle
task Demo(type: JavaExec) {
    main = "eu.hansolo.regulators.Launcher"
    classpath = sourceSets.main.runtimeClasspath
}

// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    if (gpgkey && gpgpassphrase) {
        useInMemoryPgpKeys(gpgkey, gpgpassphrase)
    }

    sign configurations.archives
    sign publishing.publications
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            group      = 'eu.hansolo'
            artifactId = 'regulators'
            version    = "${version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().dependencies.'*'.findAll {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.remove(it.classifier)
                }
            }

            pom {
                name        = 'regulators'
                description = 'Regulators is a little JavaFX library of controls'
                url         = "https://github.com/HanSolo/regulators/wiki"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url  = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id    = "HanSolo"
                        name  = "Gerrit Grunwald"
                        email = "han.solo@mac.com"
                    }
                }
                scm {
                    connection          = "scm:git:https://HanSolo/regulators"
                    developerConnection = "scm:git:https://HanSolo/regulators"
                    url                 = "https://github.com/HanSolo/regulators"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url  = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = null == ciOssrhUsername ? ossrhUsername : ciOssrhUsername
                password = null == ciOssrhPassword ? ossrhPassword : ciOssrhPassword
            }
        }
        maven {
            name = "GitHubPackages"
            url  = "https://maven.pkg.github.com/HanSolo/regulators"
            credentials {
                username = null == ciGHUser ? ghUser : ciGHUser
                password = null == ciGHToken ? ghToken : ciGHToken
            }
        }
    }
}
